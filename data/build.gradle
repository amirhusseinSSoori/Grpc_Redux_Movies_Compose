plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
}
apply from: "${rootProject.projectDir}/dependencies/gradles/dependency.gradle"
apply from: "${rootProject.projectDir}/dependencies/gradles/common.gradle"
protobuf {
    protoc {
        if (osdetector.os == "osx") {
            artifact = 'com.google.protobuf:protoc:3.12.0:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.12.0'
        }
    }
    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.38.0:osx-x86_64'
            } else {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.35.0'
            }

        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }

}
android {
    with commonConfig

    defaultConfig {
        minSdk 21
        targetSdk 32
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    with domain

    testImplementation test.junit
    androidTestImplementation test.test_ext

    //coroutine
    implementation coroutine.android

    //hilt
    implementation hilt.android
    kapt hilt.compiler

    //grpc
    implementation grpc.values()
    compileOnly grpc_util.annotations

}